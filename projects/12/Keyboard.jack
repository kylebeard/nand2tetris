// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {
    static int kbAddr;

    /** Initializes the keyboard. */
    function void init() {
        let kbAddr = 24576;
        return;
    } 

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        return Memory.peek(kbAddr);
    }

    /**								
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character 
     * of the pressed key.
     */
    function char readChar() {
        var char rv;
        let rv = 0;
        while (rv = 0){
            let rv = Keyboard.keyPressed();
        }

        while (Keyboard.keyPressed() = rv){}
        do Output.printChar(rv);
        return rv;
    }

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) {
        var String entered;
        var char currChar;
        let entered = String.new(64);
        do Output.printString(message);

        while (true) {
            let currChar = Keyboard.readChar();
        
            if (currChar = 128){
                return entered;
            }
            if (currChar = String.backSpace()) {
                do entered.eraseLastChar();
            } else {
                do entered.appendChar(currChar);
            }
        }
        return entered;
    }   

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces. 
     */
    function int readInt(String message) {
        var String val;
        var char currChar;
       
        let val = String.new(100);
      
        do Output.printString(message);
        while (true) {
            let currChar = Keyboard.readChar();

             if (currChar = 128){
                return val.intValue();
            }

            if (currChar = String.backSpace()){
                do val.eraseLastChar();  
            } else {
                do val.appendChar(currChar);
            }
        }
        if (~(currChar = String.newLine())){
            do Output.println();
        }

        return val.intValue();
    }
}
